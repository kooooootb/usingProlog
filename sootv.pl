% Пусть три друга – Антон, Никита и Александр – заняли первое, второе и 
% третье место в олимпиадах, при том все они имеют разную 
% национальность, имя и любимый учебный предмет. Антон занимается 
% математикой и занял место выше белоруса. Эстонец Никита знает свой 
% предмет лучше, чем участник олимпиады по физике свой. А любитель
% русского языка занял первое место. 

национальность(эстонец).
национальность(русский).
национальность(белорус).

имя(антон).
имя(александр).
имя(никита).

предмет(математика).
предмет(физика). 
предмет(русский_язык).

место(1).
место(2).
место(3).

принадлежит(X, [X | Хвост]).
принадлежит(X, [Голова | Хвост]):- принадлежит(X, Хвост).

уникальный([]).
уникальный([Голова | Хвост]):- \+ принадлежит(Голова, Хвост), уникальный(Хвост).

решение(Ответ):-
	% Общий вид выходной переменной
	Ответ = [уч(X, XНац, XПр, XМ), уч(Y, YНац, YПр, YМ), уч(Z, ZНац, ZПр, ZМ)],

	% Соответствие переменных характеристике
	имя(X), имя(Y), имя(Z), уникальный([X, Y, Z]),
	национальность(XНац), национальность(YНац), национальность(ZНац), уникальный([XНац, YНац, ZНац]),
	предмет(XПр), предмет(YПр), предмет(ZПр), уникальный([XПр, YПр, ZПр]),
	место(XМ), место(YМ), место(ZМ), уникальный([XМ, YМ, ZМ]),
	
	% Антон занимается математикой
	принадлежит(уч(антон, _, математика, Место_антона), Ответ),
	
	% Антон занял место выше белоруса
	принадлежит(уч(_, белорус, _, Место_белоруса), Ответ), Место_антона < Место_белоруса,
	
	% Никита - эстонец
	принадлежит(уч(никита, эстонец, _, Место_никиты), Ответ),
	
	% Никита знает свой предмет лучше, чем физик свой
	принадлежит(уч(_, _, физика, Место_физика), Ответ), Место_никиты < Место_физика,
	
	% участник олимпиады по русскому языку занял первое место
	принадлежит(уч(_, _, русский_язык, 1), Ответ).
	
задача(Ответ):-
	решение(Ответ), Ответ = [уч(_, _, _, 1), уч(_, _, _, 2), уч(_, _, _, 3)].
